# Рядки Mixins

Вираз `mixin` приймає довільний рядок, компілює його і генерує
відповідні інструкції. Це механізм **часу компіляції**, який може
працювати виключно з рядками, доступних під час компіляції. Саме тому
порівняння зі злим JavaScript `eval` було б вкрай несправедливим.

    mixin("int b = 5");
    assert(b == 5); // добре компілюється

`mixin` також працює з рядками, які будуються динамічно, допоки потрібна
інформація не залежить від значень часу виконання.

`mixin` разом з **ОФЧК (CTFE)** з наступного розділу дозволяє писати
вражаючі бібліотеки, такі як [Pegged](https://github.com/PhilippeSigaud/Pegged),
які генерують програму граматичного аналізу на основі граматики,
визначеної, як рядок у вихідному коді.

### Поглиблення

- [Mixins in D](https://dlang.org/spec/template-mixin.html)

## {SourceCode}

```d
import std.stdio : writeln;

auto calculate(string op, T)(T lhs, T rhs)
{
    return mixin("lhs " ~ op ~ " rhs");
}

void main()
{
    // Абсолютно новий підхід до Hello World!
    mixin(`writeln("Hello World");`);

	// передаємо операцію для виконання, як
    // параметр шаблону.
    writeln("5 + 12 = ", calculate!"+"(5,12));
    writeln("10 - 8 = ", calculate!"-"(10,8));
    writeln("8 * 8 = ", calculate!"*"(8,8));
    writeln("100 / 5 = ", calculate!"/"(100,5));
}
```
