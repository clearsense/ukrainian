# Шаблони

**D** дозволяє визначати шаблонні функції, аналогічні функціям C ++ і Java
яки представляють собою засiб для визначення **загальних**  функцій або об'єктiв, які працюють
для будь-якого типу, який успішно компілюється з опрераторами всередині тіла функції:


    auto add(T)(T lhs, T rhs) {
        return lhs + rhs;
    }

Параметр шаблону `Т` визначається в наборі дужок
перед фактичним параметрам функції. `Т` є заповнювачем
який замінюється компілятором коли функція насправді *інстанцюється* за допомогою оператора `!`:


    add!int(5, 10);
    add!float(5.0f, 10.0f);
    add!Animal(dog, cat); // не скомпiлюється; Animal не iмплементує +

Якщо параметр шаблона не задан для шаблонной функції, компілятор
намагається вивести тип, використовуючи вхідні параметри, з якими функція
викликається:

    int a = 5; int b = 10;
    add(a, b); // T is to deduced to `int`
    float c = 5.0f;
    add(a, c); // ПОМИЛКА: конфлікт, тому що компілятор
               // не знає яким повинен бути тип T

Функція може мати будь-яку кількість параметрів шаблону, яки
визначаються в момент створення екземпляра за допомогою синтаксiсу `func!(T1, T2 ..)`.
Параметри шаблону можуть бути будь-якого базового типу,
в тому числі `string`-ами та числами з плаваючою комою.

На відміну від дженериків в Java, шаблони в D iнстанцюються тiльки пiд час компіляції, приводячи
до високооптимізованого коду з урахуванням конкретного використанного набору типів, пiд час виклику функції.

Звичайно, типи `struct`,` `class` і `interface` теж можуть бути визначені як шаблоннi
типи.

    struct S(T) {
        // ...
    }

### Детальнiше

- [Підручник по шаблонах D](https://github.com/PhilippeSigaud/D-templates-tutorial)
- [Шаблони у _Programming in D_](http://ddili.org/ders/d.en/templates.html)

#### Розширно

- [Специфiкацiя на шаблони у D](https://dlang.org/spec/template.html)
- [Знову про шаблони](http://dlang.org/templates-revisited.html):  Walter Bright пише про те, як полипшенi шаблони у D в порiвняннi з C++.
- [Variadic-шаблони](http://dlang.org/variadic-function-templates.html): Стаття про ідіоми реалізації variadic функцій за допомогою variadic шаблонiв у D.

## {SourceCode}

```d
import std.stdio;

/**
Клас-шаблон, який дозволяє
загальну реалізацію тварин.

Params:
    noise = рядок до виводу
*/
class Animal(string noise) {
    void makeNoise() {
        writeln(noise ~ "!");
    }
}

class Dog: Animal!("Bark") {
}

class Cat: Animal!("Meeoauw") {
}

/**
Шаблонна функція, яка приймає будь-який
тип, який реалізує функцію makeNoise.
Params:
    animal = об'єкт, який може видавати шум
    n = число викликiв makeNoise
*/
void multipleNoise(T)(T animal, int n) {
    for (int i = 0; i < n; ++i) {
        animal.makeNoise();
    }
}

void main() {
    auto dog = new Dog;
    auto cat = new Cat;
    multipleNoise(dog, 5);
    multipleNoise(cat, 5);
}
```
